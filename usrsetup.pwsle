// EarthOS multi-user setup
//
//    Copyright (C) 2021  adazem009
//
// This executable registers users with the kernel.
//
//    This program is free software: you can redistribute it and/or modify
//    it under the terms of the GNU General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//
//    This program is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//    GNU General Public License for more details.
//
//    You should have received a copy of the GNU General Public License
//    along with this program.  If not, see <https://www.gnu.org/licenses/>.
//
// User must be root
if/[%whoami != "root"]
	print/"Only root can do that!",\n
	exit
endif
// Read /lib/system/users/
ls/"/lib/system/users"/"USERS"
set/"ret",%returnc
if/[ret != ""]
	geterror/ret,0/"ret"
	print/"/lib/system/users: ",ret,\n,"Rebooting in 5 seconds...",\n
	wait/5
	reboot
endif
// Check if root exists
getindex/"USERS","root"/"ix"
if/[ix == 0]
	// Create missing root user
	print/\c255000000,"N: ",\c255255255,"'root' user not found, creating",\n
	mkdir/"/lib/system/users/root"/"rw-rw----","root","root"
	set/"ret",%returnc
	if/[ret != ""]
		geterror/ret,0/"ret"
		print/"Error: ",ret,\n,"Rebooting in 5 seconds...",\n
		wait/5
		reboot
	endif
endif
// Register users
getlistlength/"USERS"/"len"
set/"i",0
repeat/len
	calc/"i=i+1"
	getitem/"USERS",i/"user"
	// root is already registered
	if/[user != "root"]
		// Register user
		adduser/user
	endif
endloop
// Check if main user exists
// (the user who owns the computer of course)
getfile/"/lib/system/main-user"/"main"
set/"ret",%returnc
if/[ret != ""]
	if/[ret == "ENOENT"]
		// This system is newly installed!
		// We should configure it first...
		print/\c255000000,"N: ",\c255255255,"main user not found, starting configuration",\n
		ftest/"/lib/system/initcfg","rx","f"/"test"
		set/"ret",%returnc
		if/[ret != ""]
			geterror/ret,0/"ret"
			print/"/lib/system/initcfg: ",ret,\n,"Rebooting in 5 seconds...",\n
			wait/5
			reboot
		endif
		getfile/"/lib/system/initcfg"/"file"
		listtobin/"file"/"bin"
		run/bin,"wait","root"
	else
		geterror/ret,0/"ret"
		print/"/lib/system/main-user: ",ret,\n,"Rebooting in 5 seconds...",\n
		wait/5
		reboot
	endif
endif
// Save changes
sync
