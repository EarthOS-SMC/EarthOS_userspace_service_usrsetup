// EarthOS initial configuration
//
//    Copyright (C) 2021  adazem009
//    Copyright (C) 2021  MESYETI
//
// Initial configuration - ask for a username and more
//
//    This program is free software: you can redistribute it and/or modify
//    it under the terms of the GNU General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//
//    This program is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//    GNU General Public License for more details.
//
//    You should have received a copy of the GNU General Public License
//    along with this program.  If not, see <https://www.gnu.org/licenses/>.
//
include/"moo.pwsl"
// User must be root
if/[%whoami != "root"]
	print/"Only root can do that!",\n
	exit
endif
// Get list of users
getusers/"USERS"
clear
print/"Welcome to EarthOS! This is a utility for setting up EarthOS for you.",\n
set/"continue",0
while/[continue == 0]
	read/"Type your new EarthOS username: ","name"
	getletterindex/name," "/"spaceIn"
	getletterindex/name,"/"/"slashIn"
	if/![spaceIn == 0] or ![slashIn == 0]
		print/"Your chosen name has characters that aren't allowed, it can't contain a space or a slash",\n
	else
		getlength/name/"len"
		// Min length: 3
		// Max length: 10
		if/[len < 3]
			print/"Username too short!",\n,"It must be at least 3 characters long.",\n
		else
			if/[len > 10]
				print/"Username too long!",\n,"Maximum allowed length is 10 characters.",\n
			else
				getindex/"USERS",name/"ix"
				if/[ix == 0]
					set/"continue",1
				else
					print/"This username is reserved or not allowed.",\n
				endif
			endif
		endif
	endif
	if/[continue == 0]
		print/\n,"Press any key to continue...",\n
		keywait/"any"
		clear
	endif
endloop
print/"Creating user..",\n
// Create the user
if/[name == "moo"]
	// Ignore this part, just some *moo things*
	moo/"... Never gonna moo you down ..."
endif
set/"path","/lib/system/users/",name
mkdir/path/"rw-rw----",name,name
setlist/"file"/name
write/"/lib/system/main-user"/"file","rw-rw----","root","root"
// Register the user
adduser/name
print/"User registered.",\n
